#+ endpoint: https://lod.globalbioticinteractions.org/query
#+ defaults:
#+   - doi: https://doi.org/10.15468/aomfnb

SELECT DISTINCT
  ?doi
  ?uuid
  ?contentId
  ?contentType
  ?archiveUrl
  ?seenAt
  ?provenanceId
  ?activity
WHERE  {
  BIND(?_doi_iri as ?doi)
  BIND(?_type as ?contentType)
  BIND(?_provenanceId_iri as ?provenanceId)
  {
    SELECT ?contentId ?seenAt ?archiveUrl ?uuid ?activity
    WHERE {
      graph ?g1 {
        ?uuid <http://www.w3.org/ns/prov#hadMember> ?archiveUrl .
        ?uuid <http://www.w3.org/1999/02/22-rdf-syntax-ns#seeAlso> ?_doi_iri .
        ?archiveUrl <http://purl.org/dc/elements/1.1/format> ?_type .
      }
      graph ?activity {
        ?activity <http://www.w3.org/ns/prov#used> ?archiveUrl .
        ?activity <http://www.w3.org/ns/prov#generatedAtTime> ?seenAt .
        ?contentId <http://www.w3.org/ns/prov#qualifiedGeneration> ?activity .
      }
    }
  }

  # exclude blank nodes that represent unavailable or non-content
  FILTER(!CONTAINS(str(?contentId), ".well-known/genid" ) )

} ORDER BY DESC(?seenAt) LIMIT 2

