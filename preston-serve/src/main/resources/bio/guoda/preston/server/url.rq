#+ endpoint: https://lod.globalbioticinteractions.org/query
#+ defaults:
#+   - url: https://hosted-datasets.gbif.org/eBird/2022-eBird-dwca-1.0.zip

SELECT DISTINCT
  ?doi
  ?uuid 
  ?contentId
  ?archiveUrl 
  ?seenAt
  ?provenanceId
  ?activity
WHERE  
{
    BIND(?_url_iri as ?archiveUrl) 
    BIND(<hash://sha256/5b7fa37bf8b64e7c935c4ff3389e36f8dd162f0705410dd719fd089e1ea253cd> as ?provenanceId)

{
    SELECT ?contentId ?seenAt ?uuid ?doi ?activity
WHERE {
  graph ?g1 {
   ?uuid <http://www.w3.org/ns/prov#hadMember> ?_url_iri .
   OPTIONAL { ?uuid <http://www.w3.org/1999/02/22-rdf-syntax-ns#seeAlso> ?doi . }
   ?_url_iri <http://purl.org/dc/elements/1.1/format> "application/dwca" .
  }
  graph ?activity {
    ?activity <http://www.w3.org/ns/prov#used> ?_url_iri .
    ?activity <http://www.w3.org/ns/prov#generatedAtTime> ?seenAt .
    ?contentId <http://www.w3.org/ns/prov#qualifiedGeneration> ?activity .  
  }

}
} 
UNION
{
    SELECT ?contentId ?seenAt ?uuid ?activity
WHERE {
   ?uuid <http://www.w3.org/ns/prov#hadMember> ?_url_iri . 
   OPTIONAL { ?uuid <http://www.w3.org/1999/02/22-rdf-syntax-ns#seeAlso> ?doi . }
   ?_url_iri <http://purl.org/dc/elements/1.1/format> "application/dwca" .
   ?activity <http://www.w3.org/ns/prov#used> ?_url_iri .
   ?activity <http://www.w3.org/ns/prov#generatedAtTime> ?seenAt .
   ?contentId <http://www.w3.org/ns/prov#qualifiedGeneration> ?activity .
}
}
  # exclude blank nodes that represent unavailable or non-content
  FILTER(!CONTAINS(str(?contentId), ".well-known/genid" ) )

} ORDER BY DESC(?seenAt) LIMIT 2

